// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or(a=load, b=reset, out=loadorreset);
    Or(a=loadorreset, b=inc, out=doload);
    Register(in=wasres, load=doload, out=regout, out=out);
    Add16(a=regout,b[0]=true, out=addout);
    Mux16(a=regout, b=addout, sel=inc, out=wasadd);
    Mux16(a=wasadd, b=in, sel=load, out=wasload);
    Mux16(a=wasload, b=false, sel=reset, out=wasres);
}
